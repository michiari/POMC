/*  Corresponding procedure in C syntax:

	void quicksort (int left,int right)
	{
		int lo,eq,hi,piv;
		if (left >= right) return;
		piv = a[right]; lo = eq = left; hi = right;
		while (lo <= hi) {
			if (a[hi] > piv) {
				hi--;
			} else {
				swap a[lo],a[hi];
				if (a[lo] < piv) {
					swap a[lo],a[eq];
					eq++;
				}
				lo++;
			}
		}
		quicksort(left,eq-1);
		quicksort(lo,right);
	}

	Ref: Schwoon, Stefan. Model-checking pushdown systems. Diss. Technische Universität München, 2002. 
	Described in: Pontiggia F., Chiari M., Pradella M.. Verification of Programs with Exceptions through Operator Precedence Automata, SEFM 2021.
*/

formulas = 	F (ret And main),
			XNu (ret And main),
			F ( G (~ aleftGTaright)),
			XNu (~ aleftGTaright),
			F (ret And main And (~ aleftGTaright)),
			XNu (ret And main And (~ aleftGTaright));
			

program:
var leftGEQright, loLThi, loEQhi, loEQleft, hiEQright, eqEQlo, eqEQleft, aleftGTaright, aleftEQaright;

main() {
	leftGEQright = false;
	randomInput();
	qs2();
}

qs2() {
	if (leftGEQright) {
	} else {
		loEQhi = false;
		loLThi = true;
		loEQleft = true;
		hiEQright = true;
		eqEQlo = true;
		eqEQleft = true;
		firstIteration2();
		while (loLThi || loEQhi) { 
			qsif2();
		}

		//recursive calls
		if (loEQleft){ 
			leftGEQright = true;
			qs2();
			leftGEQright = false;
			qs2();
		} else { 
			if (hiEQright && eqEQlo) { 
				leftGEQright = false;
				qs2();
				leftGEQright = true;
				qs2();
			} else {
				if (hiEQright && eqEQleft) { 
					leftGEQright = true;
					qs2();
					leftGEQright = true;
					qs2();
				} else {
					if (hiEQright) { 
						leftGEQright = true;
						qs2();
						leftGEQright = true;
						qs2();
					} else {
						if (eqEQleft) { 
							leftGEQright = true;
							qs2();
							leftGEQright = true;
							qs2();
						} else {
							if (eqEQlo) { 
								leftGEQright = true;
								qs2();
								leftGEQright = true;
								qs2();
							} else {}
						} 
					}
				}				
			}
		}
	}
}

qsif2() { 
	// this if-condition is due to swapping of firstIteration
	if ((! aleftGTaright) && (! aleftEQaright)) {
		shiftloLEQhi2();
		hiEQright = false;
	} else {
		if (! loEQhi){
			swapElements();
		} else {}
		
		if (aleftGTaright) {
			if (! eqEQlo){
				swapElements();
			} else {}
			eqEQleft = false;
		} else {
			eqEQlo = false;
		}

		shiftloLEQhi2();
		loEQleft = false;		
	}
}

firstIteration2() {
	swapElements();
	//lo++;
	eqEQlo = false; 
	shiftloLEQhi2();
	loEQleft = false;
}

shiftloLEQhi2() {
	if (loEQhi) {
		loEQhi = false;
		loLThi = false;					
	} else {
		loEQhi = true;
		loLThi = false;
	}
}

randomInput() {
	if (*) {
		aleftGTaright = true;
		aleftEQaright = false;
	} else {
		if (*) {
			aleftGTaright = false;
			aleftEQaright = true;
		} else {
			aleftGTaright = false;
			aleftEQaright = false;
		}
	}
}

swapElements() {
	if (aleftEQaright) {
	} else {
		aleftGTaright = !aleftGTaright;
	}
}